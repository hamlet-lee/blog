# ANTLR 任务与Gradle集成
```groovy

apply plugin: 'antlr'

// 禁用默认的任务
generateGrammarSource {
	exclude ("**/*")
}

// reference: https://github.com/gradle/gradle/issues/4727
task generateEval (type: AntlrTask) {
	final PARSER_PACKAGE_NAME = 'com.xxx.expr'
	arguments += ["-visitor", "-no-listener", "-package", PARSER_PACKAGE_NAME]
	outputs.cacheIf { true }
	source = sourceSets.main.antlr
	include ("**/Expr*.g4")
	outputDirectory = file("${project.buildDir}/generated-src/antlr/main")
}
generateGrammarSource.dependsOn(generateEval)

// 测试增加antlr4的package
task generateTryAdd (type: AntlrTask) {
	final PARSER_PACKAGE_NAME = 'com.xxx.tryadd'
	arguments += ["-visitor", "-no-listener", "-package", PARSER_PACKAGE_NAME]
	outputs.cacheIf { true }
	source = sourceSets.main.antlr
	include ("**/TryAdd*.g4")
	outputDirectory = file("${project.buildDir}/generated-src/antlr/main")
}
generateGrammarSource.dependsOn(generateTryAdd)


dependencies {
	antlr "org.antlr:antlr4:4.7"
	compile group: 'org.antlr', name: 'antlr4-runtime', version:'4.7'
	compile group: 'org.antlr', name: 'antlr4', version:'4.7'
	compile group: 'org.antlr', name: 'ST4', version:'4.0.4'
}


sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
	options.encoding = 'UTF-8'
}

```

.g4 文件放在
  * src/main/antlr/com/xxx/expr/Expr.g4
  * src/main/antlr/com/xxx/tryadd/TryAdd.g4

执行
```shell
./gradlew generateGrammarSource
```

生成的文件放在：

* build/generated-src/antlr/main/com/xxx/expr/*.java
* build/generated-src/antlr/main/com/xxx/tryadd/*.java
  
